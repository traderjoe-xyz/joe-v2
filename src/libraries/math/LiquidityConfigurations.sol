// SPDX-License-Identifier: MIT

pragma solidity 0.8.10;

import "./PackedUint128Math.sol";
import "./Decoder.sol";

/**
 * @title Liquidity Book Liquidity Configurations Library
 * @author Trader Joe
 * @notice This library contains functions to encode and decode the config of a pool and interact with the encoded bytes32.
 */
library LiquidityConfigurations {
    using Decoder for bytes32;
    using PackedUint128Math for bytes32;
    using PackedUint128Math for uint128;

    error LiquidityConfigurations__InvalidConfig();

    uint256 private constant _OFFSET_DISTRIBUTION_Y = 24;
    uint256 private constant _OFFSET_DISTRIBUTION_X = 88;

    uint256 private constant _PRECISION = 1e18;

    /**
     * @dev Encode the distributionX, distributionY and id into a single bytes32
     * @param distributionX The distribution of the first token
     * @param distributionY The distribution of the second token
     * @param id The id of the pool
     * @return config The encoded config as follows:
     * [0 - 24[: id
     * [24 - 88[: distributionY
     * [88 - 152[: distributionX
     * [152 - 256[: empty
     */
    function encodeParams(uint64 distributionX, uint64 distributionY, uint24 id)
        internal
        pure
        returns (bytes32 config)
    {
        assembly {
            config := or(shl(_OFFSET_DISTRIBUTION_Y, distributionX), or(shl(_OFFSET_DISTRIBUTION_X, distributionY), id))
        }
    }

    /**
     * @dev Decode the distributionX, distributionY and id from a single bytes32
     * @param config The encoded config as follows:
     * [0 - 24[: id
     * [24 - 88[: distributionY
     * [88 - 152[: distributionX
     * [152 - 256[: empty
     * @return distributionX The distribution of the first token
     * @return distributionY The distribution of the second token
     * @return id The id of the bin to add the liquidity to
     */
    function decodeParams(bytes32 config)
        internal
        pure
        returns (uint64 distributionX, uint64 distributionY, uint24 id)
    {
        assembly {
            distributionX := shr(_OFFSET_DISTRIBUTION_Y, config)
            distributionY := shr(_OFFSET_DISTRIBUTION_X, config)
            id := config
        }

        if (uint256(config) > type(uint152).max || distributionX > _PRECISION || distributionY > _PRECISION) {
            revert LiquidityConfigurations__InvalidConfig();
        }
    }

    /**
     * @dev Get the amounts and id from a config and amountsIn
     * @param config The encoded config as follows:
     * [0 - 24[: id
     * [24 - 88[: distributionY
     * [88 - 152[: distributionX
     * [152 - 256[: empty
     * @param amountsIn The amounts to distribute as follows:
     * [0 - 128[: x1
     * [128 - 256[: x2
     * @return amounts The distributed amounts as follows:
     * [0 - 128[: x1
     * [128 - 256[: x2
     * @return id The id of the bin to add the liquidity to
     */
    function getAmountsAndId(bytes32 config, bytes32 amountsIn) internal pure returns (bytes32, uint24) {
        (uint64 distributionX, uint64 distributionY, uint24 id) = decodeParams(config);

        (uint128 x1, uint128 x2) = amountsIn.decode();

        assembly {
            x1 := div(mul(x1, distributionX), _PRECISION)
            x2 := div(mul(x2, distributionY), _PRECISION)
        }

        return (x1.encode(x2), id);
    }
}
